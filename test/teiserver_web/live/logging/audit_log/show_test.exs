defmodule TeiserverWeb.Logging.AuditLogLive.ShowTest do
  alias Teiserver.Helpers.GeneralTestLib
  alias Teiserver.LoggingFixtures

  use TeiserverWeb.ConnCase, async: true

  describe "access control" do
    test "cannot access show page without authenticating" do
      {:ok, kw} = GeneralTestLib.conn_setup([], [:no_login])
      {:ok, conn} = Keyword.fetch(kw, :conn)

      {:error, {:redirect, %{to: path}}} =
        live(conn, ~p"/logging/audit_logs/123")

      assert path == ~p"/login"
    end

    test "cannot access show page when unauthorized" do
      {:ok, kw} = GeneralTestLib.conn_setup(["Overwatch"])
      {:ok, conn} = Keyword.fetch(kw, :conn)

      {:error, {:redirect, %{to: path}}} =
        live(conn, ~p"/logging/audit_logs/123")

      assert path == ~p"/"
    end

    test "can access show page when authorized" do
      {:ok, kw} = GeneralTestLib.conn_setup(["Admin"])
      {:ok, conn} = Keyword.fetch(kw, :conn)
      audit_log = LoggingFixtures.audit_log_fixture()

      {:ok, _live, _html} = live(conn, ~p"/logging/audit_logs/#{audit_log.id}")
    end
  end

  describe "rendering data" do
    setup [:auth]

    # For reasons unknown this test fails despite being made by the generator
    # at this stage I'm happy for it to fail and address it later, might be
    # the newer version of phoenix solves it and we're trying to do things
    # across two different versions
    @tag :needs_attention
    test "redirect on no log", %{conn: conn} do
      {:error,
       {:redirect,
        %{
          to: "/logging/audit_logs",
          flash: _flash
        }}} =
        live(conn, ~p"/logging/audit_logs/123456789")
    end

    test "render log", %{conn: conn} do
      audit_log = LoggingFixtures.audit_log_fixture()

      {:ok, _live, html} = live(conn, ~p"/logging/audit_logs/#{audit_log.id}")

      assert html =~ ~s(Audit Log: #{audit_log.id})
    end
  end

  defp auth(_state) do
    TeiserverTestLib.admin_permissions()
    |> GeneralTestLib.conn_setup()
    |> TeiserverTestLib.conn_setup()
  end
end
